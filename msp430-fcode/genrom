#!/usr/bin/perl -w

# genrom : convert the .4th files into 'F' bytecodes
#
# Copyright (c) 2024 Charles Suresh <romforth@proton.me>
# SPDX-License-Identifier: AGPL-3.0-only
# Please see the LICENSE file for the Affero GPL 3.0 license details

use strict;

use Getopt::Long;
my ($gendict,$gendefs)=(0,0);
GetOptions(
	'dict' => \$gendict,
	'gendefs' => \$gendefs,
);

my $mf=shift;   # the map file to which new defs are written

open(my $mh, ">$mf") or die "can't open $mf for writing";

my $map={};	# mapping from forth words to assembly labels
my $mapch={};	# mapping from forth words to byte codes in F
my $varmap={};	# mapping from forth words to variables
my $defs={};	# mapping from forth words to definition labels

my ($t,$f,$l);	# the type($t), forth name($f) and the assembler label ($l)
my $c;		# the bytecode in F

sub code {
	my ($f,$l,$c)=@_;
	$map->{$f}=$l;	# generate the mapping from name to label
	$mapch->{$f}=$c; # generate the mapping from name to bytecode
}
sub var {
	my ($f,$l)=@_;
	$varmap->{$f}=$l;
}
sub olddef {
	my ($f,$l)=@_;
	$defs->{$f}=$l;
}

my $prev="0";
my $label="lbl000";
sub savelabel {
	my ($f,$l)=@_;
	if ($f eq "fake") {
		$label=$l;
	} elsif ($f eq "prev") {
		$prev=$l;
	} else {
		die "unknown label";
	}
}

my $handle={
	'code' => \&code,
	'var' => \&var,
	'def' => \&olddef,
	'label' => \&savelabel,
};
my @args=@ARGV;	# all the args are assumed to have name to label mappings
for my $a (@args) {
	@ARGV=($a);		# read each of the args passed in as a file
	while(<>) {		# reading one line at a time
		($t,$f,$l,$c)=split;# split'ing the line into type, name, label
					# and bytecode in F
		$handle->{$t}($f,$l,$c); # and handle each type separately
	}
}

# state event next      action
# 0     def{    1       -
# 0     imm{    1       $immediate=1
# 1     $w      2       $label: ; $w
# 2     }def    0       .byte exit
# 2     }imm    0       .byte exit ; $immediate=0

my $state=0;
my $immediate=0;

my $mapjmp=$mapch->{'j'};
my $mapjz=$mapch->{'jz'};
my $mapjnz=$mapch->{'jnz'};

my @labelstack;

sub runbegin {
	push @labelstack, [$label++, "begin"];
	my $ol=pop @labelstack;
	print join('_', @$ol), ":\n";
	$ol->[1]="endloop";
	push @labelstack, $ol;
}

sub rununtil {
	print "\t.byte '", chr($mapjnz), "' # branch if not zero\n";
	my $ol=pop @labelstack;
	print "\tPATCH ", join('_', @$ol), "\n";
	push @labelstack, $ol;
}

sub runwhile {
	print "\t.byte '", chr($mapjz), "' # branch if zero\n";
	my $ol=pop @labelstack;
	print "\tPATCH ", join('_', @$ol), "\n";
	push @labelstack, $ol;
}

sub runrepeat {
	my $ol=pop @labelstack;
	print "\t.byte '", chr($mapjmp), "' # branch\n";
	print "\tPATCH ", $ol->[0], '_begin', "\n";
	print join('_', @$ol), ":\n";
}

sub runif {
	print "\t.byte '", chr($mapjz), "' # branch if zero\n";
	print "\tPATCH ", $label, '_then', "\n";
	push @labelstack, [$label++, "then"];
}

sub runelse {
	my $ol=pop @labelstack;
	print "\t.byte '", chr($mapjmp), "' # branch\n";
	print "\tPATCH ", $ol->[0], '_endif', "\n";
	die unless ($ol->[1] eq "then");
	print join('_', @$ol), ":\n";
	$ol->[1]="endif";
	push @labelstack, $ol;
}

sub runthen {
	my $ol=pop @labelstack;
	print join('_', @$ol), ":\n";
}

sub genprim {
	my ($f)=@_;
	my $m=$mapch->{$f};
	die "$f has no mapping" unless $m;
	print "\t.byte $m # $f\n";
}

sub runfor {
	runbegin();
	for my $i (qw(over over >r >r -)) {
		genprim($i);
	}
	runwhile();
}

sub runendfor {
	for my $i (qw(r> inc r>)) {
		genprim($i);
	}
	runrepeat();
	for my $i (qw(r> r> 2drop)) {
		genprim($i);
	}
}

my $imm={
	'loop{'		=> \&runbegin,
	'}until{'	=> \&rununtil,
	'}while{'	=> \&runwhile,
	'}loop'		=> \&runrepeat,

	'if{'		=> \&runif,
	'}else{'	=> \&runelse,
	'}if'		=> \&runthen,

	'for{'		=> \&runfor,
	'}for'		=> \&runendfor,
};

sub def {
	my ($w)=@_;
	if ($state == 0) {
		if ($w eq 'def{') {
			$state=1;
			return 1;
		}
		if ($w eq 'imm{') {
			$state=1;
			$immediate=1;
			return 1;
		}
		return 0;
	}
	if ($state == 1) {
		$l=$label++;
		print $mh "def $w $l\n"; # create mapping in "dictionary"
		if ($gendict) {
			my $len=length($w);
			print "\t.align 1\n";
			if ($len%2==0) {
				print "$l","_pad:\t.byte 0\n";
			}
			print qq{\t.ascii "$w"\n};
			$len |= 0x80 if ($immediate);
			print "$l","_nfa:\t.byte $len\n";
			print "$l","_lfa:\t.word $prev\n";
			$prev="$l"."_lfa";
		}
		print "$l:	# $w\n"; # and generate assembly code
		$defs->{$w}=$l;		 # for setting up a local mapping
		$state=2;
		return 1;
	}
	if ($state == 2) {
		if ($w eq '}def') {
			print "\t.byte '}'\n";
			$state=0;
			return 1;
		}
		if ($w eq '}imm') {
			print "\t.byte '}'\n";
			$state=0;
			$immediate=0;
			return 1;
		}
		return 0;
	}
	die "received $w in state $state";
}

sub lit2 {
	my ($o)=@_;
	print qq(\t.byte '2'\n);
	print qq(\t.word $o\n);
}

sub lit {
	my ($o)=@_;
	if (($o > -256) and ($o < 255)) { # x is in the range [-256, 256)
		print qq(\t.byte '1'\n);
		print qq(\t.byte $o\n);
	} else {	# assume everything else fits in 2 bytes
		print qq(\t.byte '2'\n);
		print qq(\t.byte ), ($o & 0xff), "\n";
		print qq(\t.byte ), ($o & 0xff00)>>8, "\n";
	}
}

my $m;					# cache for the map from name to label
while (<>) {				# map forth words in the standard input
	for $f (split) {		# for each word to their equivalent
		if ($m=$map->{$f}) {	# byte offset within the binary
			print "\t.byte '", chr($mapch->{$f}), "' # $f\n";
		} elsif ($m=$varmap->{$f}) {	# variables just need to
			lit2($m);		# push their address
		} elsif ($m=$defs->{$f}) { # definitions just need their
			print "\t.byte '{'\n";
			print "\t.word $m # $f\n";	# starting address
		} elsif ($m=$imm->{$f}) { # immediates need to be run
			$m->();		  # well, immediately
		} else {		# or whine if the mapping doesn't exist
			last if ($f eq '[');	# skip to end of line
			next if def($f);
			if ($f =~/^\'(.)\'$/) {	# char in single quotes
				lit(ord($1)); # to get the ascii value
				next;
			} elsif ($f =~/^(-?\d+)$/) {
				lit(int($1)); # atoi equivalent
				next;
			} elsif ($f =~/^(-?0x[\dA-Fa-f]+)$/) {
				lit(oct($1)); # hex conversion
				next;
			} elsif ($f=~/^\#(-?\d+)$/) {
				my $i=int($1); # "immediate" assembly equivalent
				print qq(\t.byte $i\n);
				next;
			}
			die "unknown word: $f"
		}
	}
}
print $mh "label fake $label\n";
if ($gendict) {
	open(my $lh, ">latest.s");
	select $lh;
	print "\t.word $prev\n";
	close $lh;
	print $mh "label prev $prev\n";
}
close $mh;
