#!/usr/bin/perl -w
use strict;

my $mf=shift;	# the map file to be generated

open(my $mh, ">$mf") or die "can't open $mf for writing";

my ($f,$l);	# the forth name($f) and the assembler label ($l)

sub mapnames {
	$_=<>;			# read in the next line of input
	($f,$l)=split;		# which provides forth<->label mapping
	s/^\s*$f//;
	s/^\s*$l//;
	print $mh "code $f $l\n";	# create dictionary entry for primitives
	print "$l:$_\n";	# generate the label for the assembler
}

while (<>) {
	if (/^code\{$/) {		# code{ : start of primitive declaration
		mapnames();		# handle forth word to label mapping
	} elsif (/^\}code$/) {		# }code :end of primitive declaration:
		print "; end of definition of $l -> $f\n";
	} elsif (/^inner\{$/) {		# inner{ : primitives that loop to inner
		mapnames();		# handle forth word to label mapping
	} elsif (/^\}inner$/) {		# }inner : end of word looping to inner
		print "next	; continue within the interpreter\n";
	} elsif (/^unary\s+(\S+)\s+(\S+)\s+(.*)$/) {	# unary operators
		($f,$l)=($1,$2);
		print $mh "code $f $l\n"; # create dictionary entry for unary
		print "$l:\n";		# generate the label for the assembler
		print "\t$3\n";
		print "\tnext\n";
	} elsif (/^binary\s+(\S+)\s+(\S+)\s+(.*)$/) {	# binary operators
		($f,$l)=($1,$2);
		print $mh "code $f $l\n"; # create dictionary entry for binary
		print "$l:\n";		# generate the label for the assembler
		print "\t_nip\n";
		print "\t$3\n";
		print "\tnext\n";
	} elsif (/^fallthru\s+(\S+)\s+(\S+)\s+(.*)$/) {	# "fall thru" operators
		($f,$l)=($1,$2);
		print $mh "code $f $l\n"; # create dictionary entry for fallthru
		print "$l:\n";		# generate the label for the assembler
		print "\t$3\n";
	} elsif (/^define\s+(\S+)\s+(.*)$/) {	# definitions
		print "%define $1 $2\n";
	} elsif (/^var\s+(\S+)\s+(\S+)\s+(.*)$/) {	# variables
		($f,$l)=($1,$2);
		print $mh "var $f $l\n";# create dictionary entry for variables
		print "$l:\n";		# generate the label for the assembler
		print "\t$3\n";		# and allocation/initial value if any
	} else {
		print;	# assembler code between code{...}code is passed thru
	}
}

close $mh;
