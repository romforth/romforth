#!/usr/bin/perl -w

# genrom : convert the .4th files into nasm friendly assembly code
#
# Copyright (c) 2022 Charles Suresh <romforth@proton.me>
# SPDX-License-Identifier: AGPL-3.0-only
# Please see the LICENSE file for the Affero GPL 3.0 license details

use strict;

my $mf=shift;   # the map file to which new defs are written

my $gendict=0;
if ($mf eq "-d") {
	$gendict=1;
	$mf=shift;   # the map file to which new defs are written
}
open(my $mh, ">$mf") or die "can't open $mf for writing";

my $map={};	# mapping from forth words to assembly labels
my $varmap={};	# mapping from forth words to variables
my $defs={};	# mapping from forth words to definition labels

my ($t,$f,$l);	# the type($t), forth name($f) and the assembler label ($l)

sub code {
	my ($f,$l)=@_;
	$map->{$f}=$l;	# generate the mapping from name to label
}
sub var {
	my ($f,$l)=@_;
	$varmap->{$f}=$l;
}
sub olddef {
	my ($f,$l)=@_;
	$defs->{$f}=$l;
}

my $prev="0";
my $label="lbl000";
sub savelabel {
	my ($f,$l)=@_;
	if ($f eq "fake") {
		$label=$l;
	} elsif ($f eq "prev") {
		$prev=$l;
	} else {
		die "unknown label";
	}
}

my $handle={
	'code' => \&code,
	'var' => \&var,
	'def' => \&olddef,
	'label' => \&savelabel,
};
my @args=@ARGV;	# all the args are assumed to have name to label mappings
for my $a (@args) {
	@ARGV=($a);		# read each of the args passed in as a file
	while(<>) {		# reading one line at a time
		($t,$f,$l)=split;# split'ing the line into type, name and label
		$handle->{$t}($f,$l); # and handle each type separately
	}
}

# state event next      action
# 0     def{    1       -
# 1     $w      2       $label: ; $w
# 2     }def    0       db exit

my $state=0;

my $mapjmp=$map->{'j'};
my $mapjz=$map->{'jz'};
my $mapjnz=$map->{'jnz'};

my @labelstack;

sub runbegin {
	push @labelstack, [$label++, "begin"];
	my $ol=pop @labelstack;
	print join('_', @$ol), ":\n";
	$ol->[1]="endloop";
	push @labelstack, $ol;
}

sub rununtil {
	print "\tdb ", $mapjnz, " ; branch if not zero\n";
	my $ol=pop @labelstack;
	print "\tdb ", join('_', @$ol), '-$-1', "\n";
	push @labelstack, $ol;
}

sub runwhile {
	print "\tdb ", $mapjz, " ; branch if zero\n";
	my $ol=pop @labelstack;
	print "\tdb ", join('_', @$ol), '-$-1', "\n";
	push @labelstack, $ol;
}

sub runrepeat {
	my $ol=pop @labelstack;
	print "\tdb ", $mapjmp, " ; branch\n";
	print "\tdb ", $ol->[0], '_begin-$-1', "\n";
	print join('_', @$ol), ":\n";
}

sub runif {
	print "\tdb ", $mapjz, " ; branch if zero\n";
	print "\tdb ", $label, '_then-$-1', "\n";
	push @labelstack, [$label++, "then"];
}

sub runelse {
	my $ol=pop @labelstack;
	print "\tdb ", $mapjmp, " ; branch\n";
	print "\tdb ", $ol->[0], '_endif-$-1', "\n";
	die unless ($ol->[1] eq "then");
	print join('_', @$ol), ":\n";
	$ol->[1]="endif";
	push @labelstack, $ol;
}

sub runthen {
	my $ol=pop @labelstack;
	print join('_', @$ol), ":\n";
}

sub genprim {
	my ($f)=@_;
	my $m=$map->{$f};
	die "$f has no mapping" unless $m;
	print "\tdb $m\n";
}

sub runfor {
	runbegin();
	for my $i (qw(over over >r >r -)) {
		genprim($i);
	}
	runwhile();
}

sub runendfor {
	for my $i (qw(r> inc r>)) {
		genprim($i);
	}
	runrepeat();
	for my $i (qw(r> r> 2drop)) {
		genprim($i);
	}
}

my $imm={
	'loop{'		=> \&runbegin,
	'}until{'	=> \&rununtil,
	'}while{'	=> \&runwhile,
	'}loop'		=> \&runrepeat,

	'if{'		=> \&runif,
	'}else{'	=> \&runelse,
	'}if'		=> \&runthen,

	'for{'		=> \&runfor,
	'}for'		=> \&runendfor,
};

sub def {
	my ($w)=@_;
	if ($state == 0) {
		if ($w eq 'def{') {
			$state=1;
			return 1;
		}
		return 0;
	}
	if ($state == 1) {
		$l=$label++;
		print $mh "def $w $l\n"; # create mapping in "dictionary"
		if ($gendict) {
			print "$l","_lfa:\tdw $prev\n";
			my $len=length($w);
			print "$l","_nfa:\tdb $len\n";
			print qq{\tdb "$w"\n};
			$prev="$l"."_lfa";
		}
		print "$l:	; $w\n"; # and generate assembly code
		$defs->{$w}=$l;		 # for setting up a local mapping
		$state=2;
		return 1;
	}
	if ($state == 2) {
		if ($w eq '}def') {
			print "\tdb exit\n";
			$state=0;
			return 1;
		}
		return 0;
	}
	die "received $w in state $state";
}

my $m;					# cache for the map from name to label
while (<>) {				# map forth words in the standard input
	for $f (split) {		# for each word to their equivalent
		if ($m=$map->{$f}) {	# byte offset within the binary
			print "\tdb ", $map->{$f}, "\n";
		} elsif ($m=$varmap->{$f}) { # variables just need to
			print "\tdb lit\n";	# push their
			print "\tdw $m\n";	# address
		} elsif ($m=$defs->{$f}) { # definitions just need to
			print "\tdb enter\n";	# be enter'ed at their
			print "\tdw $m\n";	# starting address
		} elsif ($m=$imm->{$f}) { # immediates need to be run
			$m->();		  # well, immediately
		} else {		# or whine if the mapping doesn't exist
			last if ($f eq '[');	# skip to end of line
			next if def($f);
			if ($f =~/^\'(.)\'$/) {	# char in single quotes
				my $o=ord($1);	# to get the ascii value
				print qq(\tdb lit\n);
				print qq(\tdw $o\n);
				next;
			} elsif ($f =~/^(-?\d+)$/) {
				my $o=int($1); # atoi equivalent
				print qq(\tdb lit\n);
				print qq(\tdw $o\n);
				next;
			} elsif ($f =~/^(-?0x[\dA-Fa-f]+)$/) {
				my $o=oct($1); # hex conversion
				print qq(\tdb lit\n);
				print qq(\tdw $o\n);
				next;
			} elsif ($f =~/^\#(-?\d+)$/) {
				my $o=int($1); # "immediate" assembly equivalent
				print qq(\tdb $o\n);
				next;
			}
			die "unknown word: $f"
		}
	}
}
print $mh "label fake $label\n";
if ($gendict) {
	open(my $lh, ">latest.s");
	select $lh;
	print "\tdw $prev\n";
	print qq{%include "dict.s"\n};
	close $lh;
	print $mh "label prev $prev\n";
}
close $mh;
