#!/usr/bin/perl -w

# genrom : convert the .4th files into C code
#
# Copyright (c) 2022-2023 Charles Suresh <romforth@proton.me>
# SPDX-License-Identifier: AGPL-3.0-only
# Please see the LICENSE file for the Affero GPL 3.0 license details

use strict;

use Getopt::Long;
my ($gendict,$gendefs)=(0,0);
GetOptions(
	'dict' => \$gendict,
	'gendefs' => \$gendefs,
);

my $mf=shift;	# the map file to which new defs are written
open(my $mh, ">$mf") or die "can't open $mf for writing";

my $map={};	# mapping from forth words to C labels
my $varmap={};	# mapping from forth words to variables
my $defs={};	# mapping from forth words to definition labels
my $deford=[];	# ordered list of definitions

my ($t,$f,$l);	# the type($t), forth name($f) and the C label ($l)

sub code {
	my ($f,$l)=@_;
	$map->{$f}=$l;	# generate the mapping from name to label
}
sub var {
	my ($f,$l)=@_;
	$varmap->{$f}=$l;
}
sub olddef {
	my ($f,$l,$d)=@_;
	$defs->{$f}=$d;
	push @$deford, [$l,$f];
}

my $prev="0";
my $label="lbl000";
sub savelabel {
	my ($f,$l)=@_;
	if ($f eq "fake") {
		$label=$l;
	} elsif ($f eq "prev") {
		$prev=$l;
	} else {
		die "unknown label";
	}
}

my $handle={
	'code' => \&code,
	'var' => \&var,
	'def' => \&olddef,
	'label' => \&savelabel,
};
my @l;
my @args=@ARGV;	# all the args are assumed to have name to label mappings
for my $a (@args) {
	@ARGV=($a);		# read each of the args passed in as a file
	while(<>) {		# reading one line at a time
		($t,$f,@l)=split;# split'ing the line into type, name and label
		$handle->{$t}($f,@l); # and handle each type separately
	}
}

# state event next      action
# 0     def{    1       -
# 0     imm{    1       $immediate=1
# 1     $w      2       $label: ; $w
# 2     }def    0       exit
# 2     }imm    0       exit ; $immediate=0

my $state=0;
my $immediate=0;

my $mapjmp=$map->{'j'};
my $mapjz=$map->{'jz'};
my $mapjnz=$map->{'jnz'};

my @labelstack;

sub runbegin {
	push @labelstack, [$label++, "begin"];
	my $ol=pop @labelstack;
	print join('_', @$ol), ":	// loop{\n";
	$ol->[1]="endloop";
	push @labelstack, $ol;
}

sub rununtil {
	print "\t", $mapjnz, ",	// }until{\n";
	my $ol=pop @labelstack;
	print "\tPATCH ", join('_', @$ol), "\n";
	push @labelstack, $ol;
}

sub runwhile {
	print "\t", $mapjz, ",	// }while{\n";
	my $ol=pop @labelstack;
	print "\tPATCH ", join('_', @$ol), "\n";
	push @labelstack, $ol;
}

sub runrepeat {
	my $ol=pop @labelstack;
	print "\t", $mapjmp, ",	// }loop\n";
	print "\tPATCH ", $ol->[0], '_begin', "\n";
	print join('_', @$ol), ":\n";
}

sub runif {
	print "\t", $mapjz, ",	// if{\n";
	print "\tPATCH ", $label, '_then', "\n";
	push @labelstack, [$label++, "then"];
}

sub runelse {
	my $ol=pop @labelstack;
	print "\t", $mapjmp, ",	// }else{\n";
	print "\tPATCH ", $ol->[0], '_endif', "\n";
	die unless ($ol->[1] eq "then");
	print join('_', @$ol), ":\n";
	$ol->[1]="endif";
	push @labelstack, $ol;
}

sub runthen {
	my $ol=pop @labelstack;
	print join('_', @$ol), ":	// }if\n";
}

sub genprim {
	my ($f)=@_;
	my $m=$map->{$f};
	die "$f has no mapping" unless $m;
	print "\t$m, // $f\n";
}

sub runfor {
	runbegin();
	for my $i (qw(over over >r >r -)) {
		genprim($i);
	}
	runwhile();
}

sub runendfor {
	for my $i (qw(r> inc r>)) {
		genprim($i);
	}
	runrepeat();
	for my $i (qw(r> r> 2drop)) {
		genprim($i);
	}
}

my $imm={
	'loop{'		=> \&runbegin,
	'}until{'	=> \&rununtil,
	'}while{'	=> \&runwhile,
	'}loop'		=> \&runrepeat,

	'if{'		=> \&runif,
	'}else{'	=> \&runelse,
	'}if'		=> \&runthen,

	'for{'		=> \&runfor,
	'}for'		=> \&runendfor,
};

my $d=0;
my $fas=[];

sub def {
	my ($w)=@_;
	if ($state == 0) {
		if ($w eq 'def{') {
			$state=1;
			return 1;
		}
		if ($w eq 'imm{') {
			$state=1;
			$immediate=1;
			return 1;
		}
		return 0;
	}
	if ($state == 1) {
		$l=$label++;
		print $mh "def $w $l $d\n"; # create mapping in "dictionary"
		if ($gendict) {
			my $len=length($w);
			print "static const struct {\n";
			if ($len%2==0) {
				print "\tchar pad;\n";
			}
			print "\tchar name\[$len\];\n";
			print "\tunsigned char nfa;\n";
			print "\tlfa *prev;\n";
			print "\tunsigned short cfa[];\n";
			print "}";
			print " ${l}={\n";
			if ($len%2==0) {
				print "\t0,\n";
			}
			push @$fas, $l;
			print "\t\"$w\",\n";
			$len |= 0x80 if ($immediate);
			print "\t$len,\n";
			print "\t(lfa *)\&$prev\.prev,\n";
			$prev=$l;
		}
		print " // $w\n";	# and generate C code
		$defs->{$w}=$d++;	# for setting up a local mapping
		push @$deford, [$l,$w];
		$state=2;
		return 1;
	}
	if ($state == 2) {
		if ($w eq '}def') {
			print "\texit\n};\n";
			$state=0;
			return 1;
		}
		if ($w eq '}imm') {
			print "\texit\n};\n";
			$state=0;
			$immediate=0;
			return 1;
		}
		return 0;
	}
	die "received $w in state $state";
}

sub lit {
	my ($o)=@_;
	print "\tlit, ";
	# adapted from perldoc -f pack
	printf("%#02x, ", $_) for unpack("W*", pack s=>$o);
	print "\n";
}

my $ref=0;
sub variable {
	my ($o)=@_;
	print "\tvar, ";
	print "\tVAR($o),\n";
}

my $m;					# cache for the map from name to label
while (<>) {				# map forth words in the standard input
	for $f (split) {		# for each word to their equivalent
		if ($m=$map->{$f}) {	# byte offset within the binary
			print "\t", $map->{$f}, ", // $f\n";
		} elsif ($m=$varmap->{$f}) {	# variables just need to
			variable($m);		# push their address
		} elsif (defined($m=$defs->{$f})) { # definitions just need
			print "\tenter, $m, // $f\n";	# their offset
		} elsif ($m=$imm->{$f}) { # immediates need to be run
			$m->();		  # well, immediately
		} else {		# or whine if the mapping doesn't exist
			last if ($f eq '[');	# skip to end of line
			next if def($f);
			if ($f =~/^\'(.)\'$/) {	# char in single quotes
				lit(ord($1)); # to get the ascii value
				next;
			} elsif ($f =~/^(-?\d+)$/) {
				lit(int($1)); # atoi equivalent
				next;
			} elsif ($f =~/^(-?0x[\dA-Fa-f]+)$/) {
				lit(oct($1)); # hex conversion
				next;
			} elsif ($f=~/^\#(-?\d+)$/) {
				my $i=int($1); # "immediate" assembly equivalent
				print qq(\t$i,\n);
				next;
			}
			die "unknown word: $f"
		}
	}
}
if ($gendefs) {
	print 'static const unsigned short *fp[] = {', "\n";
	for my $i (@$deford) {
		print "\t$i->[0]\.cfa,	// $i->[1]\n";
	}
	print "};\n";
}
print $mh "label fake $label\n";
if ($gendict) {
	open(my $lh, ">latest.h");
	select $lh;
	print "\tvars.latest=(lfa *)&$prev\.prev;\n";
	close $lh;
	print $mh "label prev $prev\n";
}
close $mh;
